#autoload

local expl oldest
local -a pubfuns

_description captasks expl "capistrano tasks"

oldest=$(( EPOCHSECONDS - (6*3600) ))

local regen=0
[[ ! -f .cap_tasks~ || config/deploy.rb -nt .cap_tasks~ || config/cap_deploy.rb -nt .cap_tasks~ ]] && regen=1
age_greater_than $oldest config/deploy.rb config/cap_deploy.rb && regen=1

if [[ -f config/deploy.rb || -f config/cap_deploy.rb ]]; then
  if [[ $regen == 1 ]]; then
    cap -T | egrep "^cap " >! .cap_tasks~
  fi
  
  local tasks
  tasks="$(< .cap_tasks~)"
  local -a targets
  targets=( ${${(f)tasks}/(#b)cap ([^ ]##) ##\# (*)/${${match[1]}//:/\\:}:${match[2]:l}} )
  if (( ! ${targets[(I)capistrano aborted!]} )) then
    _describe -t targets 'cap target' targets && ret=0
  else
    _message -e targets 'cap target'
  fi

  # compadd "$expl[@]" - `cat .cap_tasks~`
fi

