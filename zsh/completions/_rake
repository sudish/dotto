#compdef rake

# rake, version 0.6.0

local curcontext="$curcontext" state line expl ret=1
typeset -A opt_args

if ! zmemoize -p zraketargets &>/dev/null; then
  zmemoize -P -D '$PWD/Rakefile' -t 300 zraketargets
fi

_arguments -C -s -S \
  '(--classic-namespace -C)'{--classic-namespace,-C}'[put Task and FileTask in the top level namespace]' \
  '(--dry-run -n)'{--dry-run,-n}'[do a dry run without executing actions]' \
  '(- *)'{--help,-H}'[display help information]' \
  \*{--libdir,-I}'[include specified directory in the search path for required modules]:library directory:_files -/' \
  \*{--rakelib,-R}'[include specified directory in the search for task files]:library directory:_files -/' \
  '(--nosearch -N)'{--nosearch,-N}'[do not search parent directories for the Rakefile]' \
  '(--no-system -G)'{--no-system,-G}'[do not search system Rakefiles for tasks]' \
  '(--system -g)'{--system,-g}'[search system Rakefiles for tasks]' \
  '(- *)'{--prereqs,-P}'[display the tasks and prerequisites, then exit]' \
  '(--quiet -q --silent -s --verbose -v)'{--quiet,-q}'[do not log messages to standard output]' \
  '(--rakefile -f)'{--rakefile,-f}'[use specified file as the rakefile]:rake file:_files' \
  \*{--require,-r}'[require specified module before executing rakefile]:library:->library' \
  '(--quiet -q --silent -s --verbose -v)'{--silent,-s}"[like --quiet, but also suppresses the 'in directory' announcement]" \
  '(- *)'{--tasks,-T}'[display the tasks and dependencies, then exit]' \
  '(--trace -t)'{--trace,-t}'[turn on invoke/execute tracing, enable full backtrace]' \
  '(- *)'{--usage,-h}'[display usage information]' \
  '(--quiet -q --silent -s --verbose -v)'{--verbose,-v}'[log message to standard output (default)]' \
  '(- *)'{--version,-V}'[display version information]' \
  '*:target:->target' && ret=0

case "$state" in
  library)
    local -a dirs
    dirs=( $(_call_program directories ruby -e 'puts\ \$:' 2>/dev/null) ${(s.:.)opt_args[-I]} )
    _wanted libraries expl library _path_files -W dirs && ret=0
  ;;
  target)
    local -a targets 
    local zrtout fname=/tmp/zrake.$$
    zraketargets -s $opt_args[(I)(-N|--nosearch)] $opt_args[(I)(-G|--no-system|--nosystem)] \
        $opt_args[(I)(-g|--system)] \
        ${(kv)opt_args[(I)(-I|--libdir)]} \
        ${(kv)opt_args[(I)(-R|--rakelib)]} \
        ${(kv)opt_args[(I)(-f|--rakefile)]} > $fname 2>/dev/null
    targets=$?
    zrtout=$(< $fname)
    rm -f $fname
    targets=( ${${(f)zrtout}/(#b)rake ([^ ]##) ##\# (*)/${${match[1]}//:/\\:}:${match[2]:l}} )
    if (( ! ${targets[(I)rake aborted!]} )) then
      _describe -t targets 'rake target' targets && ret=0
    else
      _message -e targets 'rake target'
    fi
  ;;
esac

return ret
