#!/bin/zsh


if [[ $dotto_vars[zconfig_booted] != 1 ]]; then
  typeset -a -g -U       zinclude_path
  typeset -a -g -U -H    zrequired_files
  typeset -A -g -U -H    zloaded_libs
  typeset -A -g -H       dotto_vars

  typeset -a -g -H       dotto_disabled_components dotto_disabled_systems

  function _dotto_newshell {
    local zcfile 
    
    FPATH=$ZCONFIGDIR/functions:$ZCONFIGDIR/completions:$FPATH

    setopt function_argzero typeset_silent
  
    for zcfile in $DOTTODIR/local/build/zsh/$ZSH_VERSION/**/*.zwc(N.); do
        [[ -f $zcfile ]] && FPATH=$zcfile:$FPATH
    done

    typeset -a funcpaths
    funcpaths=("$ZCONFIGDIR/functions/*(.N)" "$DOTTODIR/external/*/zsh/functions/*(.N)")
    set -A funcpaths ${~funcpaths}
    typeset -a funcnames
    set -A funcnames ${${funcpaths%.zsh}:t}

    # load post-bootstrap version
    unfunction zctrace
    autoload -Uk "${(@)funcnames}"

    # globally used variables
    zinclude_path=("$ZCONFIGDIR")
    # for zcfile in 

    # load default prefs and local prefs
    zrunsystem prefs
    [[ -f $DOTTODIR/prefs ]] && source $DOTTODIR/prefs
    [[ -f $DOTTODIR/zsh/prefs ]] && source $DOTTODIR/zsh/prefs
    for zcfile in $DOTTODIR/external/*/prefs(.N) \
                  $DOTTODIR/external/*/zsh/prefs(.N) \
                  $DOTTODIR/external/*/users/$USERNAME/zsh/prefs(.N) \
                  $DOTTODIR/local/prefs \
                  $DOTTODIR/local/zsh/prefs
    do
      [[ -f $zcfile ]] && source $zcfile
    done

    zrunsystem newshell
  
    dotto_vars[zconfig_booted]=1
  }

  _dotto_newshell
  unfunction _dotto_newshell
fi
