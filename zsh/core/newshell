#!/bin/zsh

typeset -a -g -U       zinclude_path
typeset -a -g -U -H    zrequired_files
typeset -A -g -H       dotto_vars  

if [[ $dotto_vars[zconfig_booted] != 1 ]]; then
  function _dotto_newshell {
    local zcfile 
    
    FPATH=$ZCONFIGDIR/functions:$FPATH
  
    setopt function_argzero typeset_silent
  
    for zcfile in `find $DOTTODIR/local/build/zsh/$ZSH_VERSION -name \*.zwc 2>/dev/null `; do
        FPATH=$zcfile:$FPATH
    done

    typeset -a funcpaths
    funcpaths=("$ZCONFIGDIR/functions/*(.N)" "$DOTTODIR/external/*/zsh/functions/*(.N)")

    for zcfile in ${~funcpaths}; do
      autoload -Uk ${zcfile:t}
    done

    unset zcfile

    # globally used variables
    zinclude_path=("$ZCONFIGDIR")
    # for zcfile in 


    # load default prefs and local prefs
    zrunsystem prefs
    [[ -f $DOTTODIR/prefs ]] && source $DOTTODIR/prefs
    [[ -f $DOTTODIR/zsh/prefs ]] && source $DOTTODIR/zsh/prefs
    for zcfile in $DOTTODIR/external/*/prefs(.N) \
                  $DOTTODIR/external/*/zsh/prefs(.N) \
                  $DOTTODIR/external/*/users/$USERNAME/zsh/prefs(.N) \
                  $DOTTODIR/local/prefs(.N)
    do
      [[ -f $zcfile ]] && source $zcfile
    done

    zrunsystem newshell
  
    dotto_vars[zconfig_booted]=1
  }

  _dotto_newshell
  unfunction _dotto_newshell
fi
