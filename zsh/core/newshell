#!/bin/zsh


if [[ $dotto_vars[zconfig_booted] != 1 ]]; then
  if [[ $DOTTO_PROFILING == 1 && $DOTTO_PROFILING_STARTED != 1 ]]; then
    source $ZCONFIGDIR/core/start_profiling
  fi

  typeset -a -g -U       zinclude_path
  typeset -a -g -U       zrequired_files
  typeset -A -g -U       zloaded_libs
  typeset -A -g -H       dotto_vars

  typeset -a -g          dotto_disabled_components dotto_disabled_systems

  typeset -g -a -U zlib_paths 
  for _zsysname in zprofile zshrc zcomp zperiodic zlogin zlogout newshell 
  do
    typeset -g -a -U ${_zsysname}_files ${_zsysname}_functions
  done
  unset _zsysname
  
  function _dotto_newshell {
    local zcfile 

    FPATH=$ZCONFIGDIR/functions:$ZCONFIGDIR/completions:$FPATH

    setopt function_argzero typeset_silent
  
    for zcfile in $DOTTODIR/local/build/zsh/$ZSH_VERSION/**/*.zwc(N.); do
        [[ -f $zcfile ]] && FPATH=$zcfile:$FPATH
    done

    typeset -a funcpaths
    funcpaths=("$ZCONFIGDIR/functions/*(.N)" "$DOTTODIR/external/*/zsh/functions/*(.N)")
    set -A funcpaths ${~funcpaths}
    typeset -a funcnames
    set -A funcnames ${${funcpaths%.zsh}:t}

    # load post-bootstrap version
    unfunction zctrace
    autoload -Uk "${(@)funcnames}"

    # globally used variables
    zinclude_path=("$ZCONFIGDIR")
    # for zcfile in 

    # load default prefs and local prefs
    zrunsystem prefs

    for zcfile in $DOTTODIR/prefs(N) \
                  $DOTTODIR/zsh/prefs(N) \
                  $DOTTODIR/external/*/prefs(.N) \
                  $DOTTODIR/external/*/zsh/prefs(.N) \
                  $DOTTODIR/external/*/users/$USERNAME/zsh/prefs(.N) \
                  $DOTTODIR/local/prefs(N) \
                  $DOTTODIR/local/zsh/prefs(N)
    do
      source $zcfile
    done

    if [[ $DOTTO_PROFILING == 1 && $DOTTO_PROFILING_STARTED != 1 ]]; then
      source $ZCONFIGDIR/core/start_profiling
    fi


    zrunsystem newshell
  
    dotto_vars[zconfig_booted]=1
  }

  _dotto_newshell
  unfunction _dotto_newshell
fi
