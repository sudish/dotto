#!/bin/zsh

typeset -g -a -H -U zprofile_files zshrc_files \
        zcomp_files zperiodic_files zlogin_files zlogout_files

function zsyssourcedir {
    emulate zsh
    local droot dir
    local systemname=$1
    local -a -U droots
    shift
    
    droots=("")
    for droot in $DOTTODIR/external/*(N); do
      [[ -d $droot/zsh ]] && droots+=${droot#$ZCONFIGDIR/}/zsh
      [[ -d $droot ]] && droots+=${droot#$ZCONFIGDIR/}
    done
    
    for dir in "${(@)*}"; do
        for droot in "${(@)droots}"; do
            [[ -n "$droot" ]] && droot="${droot%/}/"
            zsourcedir -s $systemname "${droot}${dir}"
        done
    done
}

function zrunsystem() {
  emulate zsh
  local systemname system zfile arrayname
  local domain="$(zdomainname)"

  local sysuser=${ZSH_PREFS_USER:-$USERNAME}

  typeset -a -U dirs
  for systemname in "${(@)*}"; do

    dirs=()
    systemname=${systemname%.d}
    system="${systemname}.d"
    
    [[ -n ${dotto_disabled_systems[(r)$system]} ]] && continue
    [[ -n ${dotto_disabled_systems[(r)$systemname]} ]] && continue

    zctrace "zrunsystem: $systemname"
            
    dirs+="$system" 
    dirs+="$system/`uname`" 
    dirs+="os/`uname`/zsh/$system" 
    [[ -n $domain ]] && dirs+="domains/$domain/zsh/$system"
    dirs+="hosts/`hostname -s`/zsh/$system" 
    dirs+="hosts/`hostname`/zsh/$system"
    dirs+="users/$sysuser/zsh/$system"
    zsyssourcedir $systemname $dirs
    
    arrayname=${systemname%.d}_files
    for zfile in ${(P)arrayname}; do
      zctrace "Sourcing $zfile"
      source $zfile
    done
    
    arrayname=${systemname%.d}_functions
    for zfile in ${(P)arrayname}; do
      zctrace "Running function '$zfile'"
      ${=zfile}
    done
  done
  
  
}
