#!/bin/zsh

typeset -g -a -H -U zprofile_files zshrc_files \
        zcomp_files zperiodic_files zlogin_files zlogout_files zsystem_done

typeset -g zsystem_current

function zsyssourcedir {
    emulate zsh
    local droot dir
    local systemname=$1
    local -a -U droots
    shift
    
    droots=("")
    for droot in $DOTTODIR/external/*(N); do
      [[ -d $droot/zsh ]] && droots+=${droot#$ZCONFIGDIR/}/zsh
      [[ -d $droot ]] && droots+=${droot#$ZCONFIGDIR/}
    done
    
    for dir in "${(@)*}"; do
        for droot in "${(@)droots}"; do
            [[ -n "$droot" ]] && droot="${droot%/}/"
            zsourcedir -s $systemname "${droot}${dir}"
        done
    done
}

function zsystem_done {
  emulate -L zsh
  local name=$1
  name=${name%.d}

  [[ -n ${zsystem_done[(r)$name]} ]]
}

function zrunsystem_libs() {
  local systemname=$1
  local arrayname zfile 
  local -a zrlfiles zrlfunctions

  arrayname=${systemname%.d}_files
  zrlfiles=(${(P)arrayname})
  set -A $arrayname

  for zfile in $zrlfiles; do
    source $zfile
  done
  
  arrayname=${systemname%.d}_functions
  zrlfunctions=(${(P)arrayname})
  set -A $arrayname
  
  for zfile in $zrlfunctions; do
    ${=zfile}
  done
}

function zrunsystem() {
  emulate zsh
  local systemname system zfile arrayname
  local domain="$(zdomainname)"
  local zsystem_current=""

  local sysuser=${ZSH_PREFS_USER:-$USERNAME}

  typeset -a -U dirs
  for systemname in "${(@)*}"; do

    dirs=()
    systemname=${systemname%.d}

    zsystem_current=$systemname
    system="${systemname}.d"
    
    [[ -n ${dotto_disabled_systems[(r)$system]} ]] && continue
    [[ -n ${dotto_disabled_systems[(r)$systemname]} ]] && continue

    zctrace "zrunsystem: $systemname"
    
    if ! zsystem_done $systemname; then
      dirs+="$system" 
      dirs+="$system/`uname`" 
      dirs+="os/`uname`/zsh/$system" 
      [[ -n $domain ]] && dirs+="domains/$domain/zsh/$system"
      dirs+="hosts/`hostname -s`/zsh/$system" 
      dirs+="hosts/`hostname`/zsh/$system"
      dirs+="users/$sysuser/zsh/$system"
      zsyssourcedir $systemname $dirs
    fi

    # execute any phased files/functions that have been set
    # since -- this is used by the 'uselib' function
    zrunsystem_libs $systemname

    zsystem_done+=$systemname
  done
}
