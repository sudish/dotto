#!/bin/zsh

typeset -g -a -H -U zprofile_files zshrc_files \
        zcomp_files zperiodic_files zlogin_files zlogout_files

function zsyssourcedir {
    emulate zsh
    local droot dir
    local -a droots
    
    droots=("")
    for droot in $DOTTODIR/external/*(N); do
        droots+=${droot#$ZCONFIGDIR/}/zsh
        droots+=${droot#$ZCONFIGDIR/}
    done
    
    for dir in "${(@)*}"; do
        for droot in "${(@)droots}"; do
            [[ -n "$droot" ]] && droot="${droot%/}/"
            zsourcedir "${droot}${dir}"
        done
    done
}

function zrunsystem() {
  emulate zsh
  local systemname system zfile arrayname
  local domain="$(zdomainname)"

  local sysuser=${ZSH_PREFS_USER:-$USERNAME}

  typeset -a dirs
  for systemname in "${(@)*}"; do
    dirs=()
    zctrace "zrunsystem: $systemname"
    system="${systemname%.d}.d"
    
    dirs+="$system" 
    dirs+="$system/`uname`" 
    dirs+="os/`uname`/zsh/$system" 
    [ -n "$domain" ] && dirs+="domains/$domain/zsh/$system"
    dirs+="hosts/`hostname -s`/zsh/$system" 
    dirs+="hosts/`hostname`/zsh/$system"
    dirs+="users/$USERNAME/zsh/$system"
    zsyssourcedir $dirs
    
    arrayname=${systemname%.d}_files
    for zfile in ${(P)arrayname}; do
      zctrace "Sourcing $zfile"
      source $zfile
    done
  done
  
  
}
