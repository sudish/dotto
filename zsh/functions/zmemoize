#!/bin/zsh

##
## Standard memoization preserves standard output (if less than XX bytes), and the
## return value.
##
## In addition to the arguments, the following components may be added to the
## memoization key:
##
##    -o <optionlist>
##        ZSH option settings; option names are expressed as space or comma separated
##        names in string form, e.g. "login,promptpercent"
##

typeset -g -H -A zmemoize_config
typeset -g -H -A zmemoize_defs
typeset -g -H -A zmemoize_cache

function _zmemoize_calculate_key {
  emulate -L zsh
  local funcname=$1 key="" funcall="" tmp=""
  typeset -A memodef addons
  shift
  REPLY=""
  
  [[ -n "$zmemoize_defs[$funcname]" ]] && zavp -d -v memodef $zmemoize_defs[$funcname]
  
  key="${funcname}:${(pj:\0:)*}"
  
  [[ $memodef[vary_pwd] == 1 ]] && addons[pwd]=$PWD
  [[ $memodef[vary_username] == 1 ]] && addons[username]=$USERNAME
  [[ $memodef[vary_hostname] == 1 ]] && addons[hostname]=$HOST
  [[ $memodef[vary_pid] == 1 ]] && addons[pid]=$$
  [[ $memodef[vary_session] == 1 ]] && dotto_session_id >/dev/null && addons[session]=$REPLY
  [[ $memodef[vary_tty] == 1 ]] && addons[tty]=$TTY
  [[ $memodef[vary_euid] == 1 ]] && addons[euid]="${EUID}"
  [[ $memodef[vary_egid] == 1 ]] && addons[egid]="${EGID}"

  addons[options]=
  if [[ -n $memodef[vary_options] ]]; then
    local option=
    for option in "${(ops:,:)memodef[vary_options]}"; do
      zctrace "Checking options: $option..."
      addons[options]+=":${option//_/}=$options[$option]:"
    done
  fi
  
  addons[files]=
  if [[ -n $memodef[vary_file] ]]; then
    local vfile= fsize= mtime=
    for vfile in "${(ops.:.)memodef[vary_file]}"; do
      # no file, mark as such
      vfile="${(e)vfile}"
      [[ ! -f $vfile ]] && { addons[files]+="(^$vfile)"; continue }
      mtime=$(builtin stat +mtime $vfile)
      fsize=$(builtin stat +size $vfile)
      addons[files]+="($file,$mtime,$fsize)"
    done
  fi 2>/dev/null

  if [[ -n $memodef[vary_function] ]] && funcall=$memodef[vary_function]; then
    REPLY=""
    reply=()
    zcapture tmp ${(z)funcall} 2>/dev/null
    addons[funcalll]="${tmp}${REPLY}${(pj.:.)reply}"

    # addons[funcall]="$(${(z)funcall} 2>/dev/null)${REPLY}${(pj.:.)reply}"
  fi
  
  [[ -n $memodef[vary_eval] ]] && addons[evalcall]=${(e)~memodef[vary_eval]} 2>/dev/null

  if [[ ${#addons} -gt 0 ]]; then
    key+=":${(pj:\0:)${(@kv)addons}}"
  fi
  
  REPLY=$key
}

function _zmemoize_capture {
  local key=$1 funcname=$2
  shift 2
  
  zlog "capturing $key..."
  local coutput cstatus varname=""
  local wrapper="_zmemoize_wrapped_$funcname"
  typeset -A memoavps memodef

  [[ -n "$zmemoize_defs[$funcname]" ]] && zavp -d -v memodef $zmemoize_defs[$funcname]

  zshellfunc_type $wrapper >/dev/null
  case $REPLY in
    alias) 
         # get actual effective command
         wrapper=${$(builtin alias $wrapper)#${wrapper}=}
         # dequote if necessary
         wrapper=${${wrapper#[\'\"]}%[\'\"]}
      ;;
  esac

  local stdout= newline= stderr= tmpfile=
  newline=$(print -l "" "tail")
  newline="${newline%tail}"

  # stderr capturing version
  if [[ $memodef[stderr] == 1 ]]; then
    tmpfile=${TMPPREFIX:-"/tmp/zsh"}.zmemoize.$$.$SECONDS
    stdout=$(${(z)wrapper} "${(@)*}" 2>! $tmpfile)
    
    if [[ -s $tmpfile ]]; then
      memoavps[has_stderr]=1
      memoavps[stderr]=$(< $tmpfile)
      memoavps[stderr]+=$newline
    fi
  else
    stdout=$(${(z)wrapper} "${(@)*}")
  fi
  
  for varname in ${(ps.:.)memodef[reply_scalars]}; do
    [[ -z $varname ]] && continue
    memoavps[reply_scalar_${varname}]=${(P)varname}
  done

  for varname in ${(ps.:.)memodef[reply_arrays]}; do
    [[ -z $varname ]] && continue
    memoavps[reply_array_${varname}]=${(pj."".)${(Pq)varname}} 
  done

  stdout+="${newline}"  
  memoavps[stdout]=$stdout
  memoavps[status]=$?

  [[ -n $memodef[timeout] ]] && memoavps[expires]=$(($EPOCHSECONDS + $memodef[timeout]))

  zavp -v memoavps >/dev/null
  zmemoize_cache[$key]=$REPLY
  
  # zlog "MEMOAVP keys are ${(k)memoavps}, stdout is $memoavps[stdout], str is $(echo $zmemoize_cache[$key] | cat -vt)" 

  echo -n -E "$memoavps[stdout]"
  [[ $memoavps[has_stderr] == 1 ]] && echo -n -E $memoavps[stderr] >&2
  return $memoavps[status]
}

#
# Clear the cache and unmemoize all memoized functions
#
function zmemoize_reset {
  typeset -g -H -A zmemoize_cache
  for key in "${(@k)zmemoize_cache}"; do
    unset "zmemoize_cache[$key]"
  done

  typeset -g -H -A zmemoize_config
  typeset -g -H -A zmemoize_defs
  
  local funcname
  for funcname in "${(@k)zmemoize_defs}"; do
    zmemoize -u $funcname
  done
}

#
# Clear all or part of the cache
#
function zmemoize_clear {
  emulate -L zsh
  local pattern=$1 key
  
  if [[ -n $pattern ]]; then
    local isdone=0
    while [[ $isdone == 0 ]]; do
      key=${zmemoize_cache[(i)$pattern]}
      if [[ -n $key ]]; then
        unset "zmemoize_cache[$key]"
      else
        isdone=1
      fi
    done
  else
    unset zmemoize_cache
    typeset -g -H -A zmemoize_cache
  fi
}

function _zmemoize_replay {
  local __funcname=$1 __key=$2 __hashname=$3
  local ref="" val="" __has_stderr=0 __varname=""

  if [[ -z $__hashname ]]; then
    typeset -A _zdecoded
    cached=$zmemoize_cache[$key]
    if [[ -z "$cached" ]]; then
      return 234
    fi
    if ! zavp -d -v _zdecoded $cached 2>/dev/null; then
      return 235
    fi
    __hashname="_zdecoded"
  fi
  
  # dear lord there has to be a prettier way to do this, but I'm
  # tired of playing line noise with zsh parameter substitution
  
  ref="${__hashname}[stdout]"
  echo -n -E "${(P)ref}"
  
  ref="${__hashname}[__has_stderr]"
  if [[ -n "${(P)ref}" ]]; then
    ref="${__hashname}[stderr]"
    echo -n -E "${(P)ref}" >&2
  fi
  
  ref="${__hashname}[(I)reply_scalar_*]"
  for __varname in ${(P)ref}; do
    ref="${__hashname}[${__varname}]"
    eval "${__varname#reply_scalar_}=\$$ref"
  done

  ref="${__hashname}[(I)reply_array_*]"
  for __varname in ${(P)ref}; do
    ref="${__hashname}[${__varname}]"
    typeset -g -a "${__varname#reply_array_}"
    set -A "${__varname#reply_array_}" ${(Q)${(Pps:"":)ref}} 
  done

  ref="${__hashname}[status]"
  return ${(P)ref}
}

function _zmemoize_wrapper {
  local key="" cached="" funcname=$1 capture=1

  _zmemoize_calculate_key $*
  key=$REPLY
  cached=$zmemoize_cache[$key]

  zctrace "wrapper: func is $1, key is $key" >&2
  zctrace "cached is ..$cached..${#cached}"
  
  ## short result means a status code; unset and return
  if [[ -n $cached ]]; then
    typeset -A zdecoded
    if zavp -d -v zdecoded $cached 2>/dev/null; then
      if [[ -n $zdecoded[expires] && $zdecoded[expires] -le $EPOCHSECONDS ]]; then
        zlog "zmemoize_wrapper: expiring $funcname:$* entry"
        capture=1
        unset "zmemoize_cache[$key]"
      else
        _zmemoize_replay $funcname $key zdecoded
        return
      fi
    fi
  fi

  if [[ $capture == 1 ]]; then
    _zmemoize_capture $key $*
  else
    $*
  fi
}

function zmemoize {
  emulate -L zsh

  local opt usage OPTIND OPTARG delete=0 verbose=0 clear=1 action=memoize require_funcname=1
  local funcname wrappername

  typeset -A memodef

  # defaults
  memodef[timeout]=1800
  memodef[stderr]=0
  memodef[vary_pwd]=0
  memodef[vary_hostname]=1
  memodef[vary_username]=1
  memodef[reply_scalars]="REPLY"
  memodef[reply_arrays]="reply"

  usage="Usage: zmemoize <options> FUNCTIONNAME
  
    Many of the options below inject additional data into the memoization key,
    and are additive. This can be used to make manifest the implicit arguments
    like environment that may be important to correct memoization of a non-pure
    function.
    
Options:
    -h   Show this help.
    -l   Print a list of memoized functions.
    -c   Clear memoization cache.  If a function name is supplied, clear the
         entries only for that funciton.
    -m   Memoize the named function (default action)
    -r   Reset entire memoization system, clearing cache and unmemoizing 
         all functions.
    -u   Unmemoize named function
    -t <seconds> 
         Memoization timeout -- note that the memoized value is not guaranteed
         to persist for this period, or indeed for any period as the cache may
         be flushed.  Default is 30 minutes (1800 seconds)
    -a <varname>
         Add an array variable name used for function return data.
    -s <varname>
         Add a scalar variable name used for function return data.
    -e   Also memoize and replay standard error output.
    -p   Print the parameters of a function
    -P   Inject current working directory (\$PWD)
    -O <optionlist>
         Inject the setting status of ZSH options given in a comma separated list
         of option names (e.g. automenu,warn_create_global,promptsubst).
    -F <functionname>
         Inject the output of a named function evaluated each time the memoized
         function is called.
    -V <varstring>
         Inject the output of expanding a string at call time.  The argument
         should be a variable reference, parameter expansion, process substitution,
         or arithmetic expansion, or some combination of those.  Examples:
            -V '\$foo'        -> the run-time value of the variable 'foo'
            -V '\$(ls -l)'    -> The output of running ls -l
            -V '\${(k)assoc}' -> the keys of the array names 'assoc'
            -V '*'           -> all files in the current directory
    -I   Inject process ID (\$\$)
    -S   Inject the dotto_session_id
    -T   Inject the TTY name
    -D <FILENAMES>
         Inject a dependency on the timestamp and size of one or more files
         which are contained in a path-like, colon separated string FILENAMEs.
         Each filename is subjected to parameter expansion at runtime.  If it
         does not contain a '\$' character, each path is converted to an absolute
         path at definition time.  e.g. (-D '/tmp/file1:$PWD/.git')
    -G   Inject the EGID (effective GID)
    -E   Inject the EUID (effective UID)
    -H   Hostname.  Set by default. May be disabled with '+H'.
    -U   Username.  Set by default. May be disabled with '+U'.
"

  local -a arg_functions arg_evals arg_options arg_files

  while getopts "a:s:cehlmprt:uvD:EF:GHIO:PSTUV:" opt; do
    case "$opt" in
      h) echo -E $usage; return 0 ;;
      m) action=memoize; require_funcname=1;;
      u) delete=1; action=unmemoize ;;
      c) action=clear; require_funcname=0 ;;
      p) action=print; require_funcname=1 ;;
      r) action=reset; require_funcname=0;;
      l) action=list
         print ${(k)zmemoize_defs}
         return
         ;;
      a) memodef[reply_arrays]+=":${OPTARG}" ;;
      s) memodef[reply_scalars]+=":${OPTARG}" ;;
      e) memodef[stderr]=1 ;;
      t) memodef[timeout]=$OPTARG ;;
      U) memodef[vary_username]=1 ;;
      H) memodef[vary_hostname]=1 ;;
      +U) memodef[vary_username]=0 ;;
      +H) memodef[vary_hostname]=0 ;;
      P) memodef[vary_pwd]=1 ;;
      I) memodef[vary_pid]=1 ;;
      S) memodef[vary_session]=1 ;;
      T) memodef[vary_tty]=1 ;;
      G) memodef[vary_egid]=1 ;;
      E) memodef[vary_euid]=1 ;;
      F) arg_functions+=$OPTARG ;;
      V) arg_evals+=$OPTARG ;;
      O) arg_options+=$OPTARG ;;
      D) arg_files+=$OPTARG ;;
      v) verbose=1 ;;
      *) echo -E $usage; return 1;;
    esac
  done
  
  # split and canonicalize paths
  if [[ $action == "memoize" && $#arg_files -gt 0 ]]; then
    local dfile= 
    typeset -a dfiles
    for dfile in $arg_files; do
      for dfile in "${(ops.:.)OPTARG}"; do
        # resolve to absolute path unless it contains a '$'
        [[ $dfile == *\$* ]] || { zrealpath $dfile >/dev/null; dfile=$REPLY }
        dfiles+=$dfile
      done
    done  
    memodef[vary_file]="${(j.:.)dfiles}"
  fi

  [[ $#arg_functions -gt 0 ]] && memodef[vary_functions]=${(j.\0.)arg_functions}
  [[ $#arg_evals -gt 0 ]] && memodef[vary_eval]=${(j.\0.)arg_evals}
  [[ $#arg_options -gt 0 ]] && memodef[vary_options]=${(j.,.)arg_options}
  
  shift $(( OPTIND - 1 ))

  if [[ $require_funcname == 1 ]] && (( $# != 1 )); then
    echo -E $usage
    return 1
  fi

  funcname=$1
  wrappername="_zmemoize_wrapped_$funcname"

  case $action in
    clear)
        zmemoize_clear $funcname
        return
      ;;
    reset) 
        zmemoize_reset
        return
      ;;
    print)
        local key=""
        typeset -A printdef
        [[ -z $zmemoize_defs[$funcname] ]] && { echo "Function $funcname not memoized." >&2; return 1 }
        
        zavp -d -v printdef $zmemoize_defs[$funcname] >/dev/null
        for key in ${(ko)printdef}; do
          echo -E "$key: ${printdef[$key]}"
        done
        return
      ;;
    memoize) ;;
    unmemoize) ;;
    *) echo "Unknown action: $action" >&2; exit 1;;
  esac

  # clear all cached bits
  [[ -n $funcname ]] || { echo "Must provide a function name"; return 1 }
  
  zmemoize_clear "${funcname}:*"

  # if deletion is requested, or function is already memoized
  if [[ $action == "unmemoize" || $delete == 1 || -n $zmemoize_defs[$funcname] ]]; then
    if [[ -n $zmemoize_defs[$funcname] ]]; then
      zctrace "unmemoizing $funcname from $wrappername"

      unset "zmemoize_defs[$funcname]"      
      unfunction $funcname

      zshellfunc_rename $wrappername $funcname || { echo "FAILED TO RENAME BACK: $?" >&2; return 21 }
    else
      [[ $action == "unmemoize" ]] && return 35
    fi
  fi
  
  if [[ $action == "memoize" ]]; then
    zctrace "memoizing $funcname to $wrappername"
  
    unfunction $wrappername &>/dev/null
    zshellfunc_rename $funcname $wrappername || { echo "FAILED TO RENAME: $?" >&2; return 20 }
    function $funcname {
      _zmemoize_wrapper $0 $*
    }
    
    zavp -v memodef >/dev/null
    zmemoize_defs[$funcname]=$REPLY
  fi
}

zmemoize_reset
