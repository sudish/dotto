#!/bin/zsh

typeset -A -g -U -H    zloaded_libs

function _zloadlib {
  local libdir=$1 lib=$2 zwc
  
  for zwc in $DOTTODIR/local/build/zsh/${ZSH_VERSION}/lib/$lib/**/*.zwc(N.); do
    fpath+=($zwc)
  done

  [[ -d $libdir/functions ]] && fpath+=($libdir/functions)  
  [[ -d $libdir/completions ]] && fpath+=($libdir/completions)  
  [[ -d $libdir/include ]] && zinclude_path+=($libdir/include)
  [[ -d $libdir/bin ]] && path+=($libdir/bin)
  [[ -d $libdir/man ]] && manpath+=($libdir/man)
  
  [[ -f $libdir/init.zsh ]] && { source $libdir/init.zsh || return }
  
  # now run the boot phase files/functions that won't get run because
  # we've already booted
  
  local bootphase=""
  for bootphase in prefs newshell zprofile zshrc zcomp zlogin; do
    zsystem_done $bootphase && zrunsystem_libs $bootphase
  done
  
  return 0
}

function _zfindlib {
  emulate -L zsh
  local lib=$1 libdir glob
  typeset -a libdirs pats

  pats=("$DOTTODIR/local/zsh/lib" "$DOTTODIR/external/*/zsh/lib(N)" \
           "$ZCONFIGDIR/lib")
  for glob in $pats; do
    for libdir in ${~glob}; do
      [[ -d $libdir/$lib ]] || continue
      echo "${libdir}/$lib" && return 0
    done
  done
  return 1
}

function uselib {
  local lib libdir

  [[ $# == 0 ]] && { echo "${(k)zloaded_libs}"; return 0 }

  for lib in "${(@)*}"; do
    [[ -n $zloaded_libs[$lib] ]] && return 0
    libdir=$(_zfindlib $lib) || { echo "Cannot find library: $lib"; return 1 }

    { _zloadlib $libdir $lib && zloaded_libs[$lib]=$libdir } || \
        { echo "Cannot load library: $lib"; return }
  done
}


