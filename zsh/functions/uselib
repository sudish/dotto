#!/bin/zsh

typeset -A -g -U -H    zloaded_libs

function _zloadlib {
  local opt="" OPTIND="" OPTARG="" reload=0
  local force=0
  
  while getopts "fr" opt; do
    case "$opt" in
      r) reload=1; loadoptions+="-r";;
      f) reload=1; loadoptions+="-r";;
      *) echo "Unknown option: $opt"; return 1;;
    esac
  done
  
  shift $(( OPTIND - 1 ))

  local libdir=$1 lib=$2 zwc="" func=""

  for zwc in $DOTTODIR/local/build/zsh/${ZSH_VERSION}/lib/$lib/**/*.zwc(N.); do
    fpath+=($zwc)
  done

  if [[ $reload == 1 ]]; then
    for func in $libdir/functions/*(.N) $libdir/completions/*(.N); do
      unfunction ${${func:t}:r} &>/dev/null
    done
  fi

  [[ -d $libdir/functions ]] && fpath+=($libdir/functions)  
  [[ -d $libdir/completions ]] && fpath+=($libdir/completions)  
  [[ -d $libdir/include ]] && zinclude_path+=($libdir/include)
  [[ -d $libdir/bin ]] && path+=($libdir/bin)
  [[ -d $libdir/man ]] && manpath+=($libdir/man)
  
  [[ -f $libdir/init.zsh ]] && { source $libdir/init.zsh || return }
  
  # now run the boot phase files/functions that won't get run because
  # we've already booted
  
  local bootphase=""
  for bootphase in prefs newshell zprofile zshrc zcomp zlogin; do
    zsystem_done $bootphase && zrunsystem_libs $bootphase
  done
  
  return 0
}

function _zfindlib {
  emulate -L zsh
  local lib=$1 libdir glob
  typeset -a libdirs pats
  REPLY=""
  pats=("$DOTTODIR/local/zsh/lib" "$DOTTODIR/external/*/zsh/lib(N)" \
           "$ZCONFIGDIR/lib")
  for glob in $pats; do
    for libdir in ${~glob}; do
      [[ -d $libdir/$lib ]] || continue
      REPLY="${libdir}/$lib" && return 0
    done
  done
  return 1
}

function uselib {
  local lib="" libdir=""
  local opt="" OPTIND="" OPTARG=""
  local force=0
  local -a loadoptions
  
  while getopts "fr" opt; do
    case "$opt" in
      r) force=1; loadoptions+="-r";;
      f) force=1; loadoptions+="-r";;
      *) echo "Unknown option: $opt"; return 1;;
    esac
  done

  shift $(( OPTIND - 1 ))

  [[ $# == 0 ]] && { print -l "${(k)zloaded_libs}"; return 0 }

  for lib in "${(@)*}"; do
    [[ $force != 1 && -n $zloaded_libs[$lib] ]] && return 0
    _zfindlib $lib && libdir=$REPLY || { echo "Cannot find library: $lib"; return 1 }

    { _zloadlib $loadoptions $libdir $lib && zloaded_libs[$lib]=$libdir } || \
        { echo "Cannot load library: $lib"; return }
  done
  
  REPLY=""
}


